apply plugin: 'com.android.application'
apply from: '../config/quality/quality.gradle'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "com.example.yen.imdb"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "consumer"

    productFlavors {
        def baseURL = "\"http://api.myapifilms.com/imdb/\""
        def timeoutInSec = "60"
        def formatJSON = "\"json\""
        def language = "\"en-us\""

        dev {
            dimension "consumer"
            applicationIdSuffix ".dev"

            timeoutInSec = "90"

            buildConfigField "String", "BASE_URL", baseURL
            buildConfigField "int", "TIMEOUT_IN_SEC", timeoutInSec
            buildConfigField "String", "FORMAT_JSON", formatJSON
            buildConfigField "String", "LANGUAGE", language
        }

        prod {
            dimension "consumer"
            applicationIdSuffix ".prod"

            buildConfigField "String", "BASE_URL", baseURL
            buildConfigField "int", "TIMEOUT_IN_SEC", timeoutInSec
            buildConfigField "String", "FORMAT_JSON", formatJSON
            buildConfigField "String", "LANGUAGE", language
        }
    }

    buildTypes {
        debug {}
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    lintOptions {
        warningsAsErrors false
        checkReleaseBuilds true
        abortOnError true
        lintConfig file("$project.rootDir/config/quality/lint/lint.xml")

        disable 'GoogleAppIndexingWarning' // App does not have deep linking.
        disable 'InvalidPackage' // Okio references java.nio that does not presented in Android SDK.
    }

    packagingOptions {
        exclude('META-INF/LICENSE')
        exclude('META-INF/NOTICE')
    }

    configurations.all { config ->
        if (config.name.contains('UnitTest')) {
            config.resolutionStrategy.eachDependency { details ->
                if (details.requested.group == 'com.squareup.leakcanary' && details.requested.name == 'leakcanary-android') {
                    details.useTarget(group: details.requested.group, name: 'leakcanary-android-no-op', version: details.requested.version)
                }
            }
        }
    }
}

dependencies {
    final BUTTERKNIFE_VERSION = '8.8.1'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"

    implementation "com.google.dagger:dagger:$DAGGER2_VERSION"
    annotationProcessor "com.google.dagger:dagger-compiler:$DAGGER2_VERSION"

    implementation "com.squareup.retrofit2:retrofit:$RETROFIT2_VERSION"
    implementation "com.squareup.retrofit2:converter-moshi:$RETROFIT2_VERSION"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'

    implementation "com.github.bumptech.glide:okhttp3-integration:$GLIDE_VERSION"
    implementation "com.github.bumptech.glide:glide:$GLIDE_VERSION"
    annotationProcessor "com.github.bumptech.glide:compiler:$GLIDE_VERSION"

    implementation "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    annotationProcessor "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$LEAKCANARY_VERSION"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$LEAKCANARY_VERSION"
    // Unit test
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$LEAKCANARY_VERSION"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.5'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    // Quality
    implementation 'com.google.code.findbugs:annotations:3.0.0'
}